plugins {
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

allprojects {
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }
}

subprojects {
    group 'com.github.michaelruocco'
    version '1.0.0'

    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.25'

        testCompile 'junit:junit:4.12'
        testCompile 'org.assertj:assertj-core:3.11.1'
        testCompile 'org.mockito:mockito-core:2.21.0'
        testCompile 'com.github.michaelruocco:file-loader:4.0.0'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
    }
}